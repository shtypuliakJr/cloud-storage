plugins {
    id 'java'
    id 'org.springframework.boot' version "${springframeworkBootVersion}"
    id 'io.spring.dependency-management' version "${springDependencyManagmentVersion}"
    id 'org.openapi.generator' version "${openapiGeneratorVersion}"
}

group = 'edu.nau.cs.file.service'
version = 'undefined'

java {
    sourceCompatibility = 17
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springdoc:springdoc-openapi-ui:${openapiVer}"
    implementation "org.openapitools:jackson-databind-nullable:${openapiJacksonVer}"
    implementation platform("software.amazon.awssdk:bom:${awssdkVersion}")
    implementation "software.amazon.awssdk:s3"
    implementation "software.amazon.awssdk:apache-client"

    compileOnly "org.projectlombok:lombok"
    runtimeOnly "org.postgresql:postgresql"

    annotationProcessor "org.projectlombok:lombok"
    testImplementation "org.springframework.boot:spring-boot-starter-test"
}

test {
    useJUnitPlatform()
}

openApiValidate {
    inputSpec = "$rootDir/src/main/resources/server-api.yml"
}

openApiGenerate {
    generatorName = "spring"
    inputSpec = "$rootDir/src/main/resources/server-api.yml"
    outputDir = "$buildDir/generated"
    apiPackage = "edu.nau.cs.file.service.controller"
    modelPackage = "edu.nau.cs.file.service.model"
    configOptions = [
            dateLibrary  : "java8",
            interfaceOnly: "true",
            useTags      : "true"
    ]
    importMappings = [
            "FileMetaDTO"            : "edu.nau.cs.file.service.dto.FileMetaDTO",
            "ErrorDTO"               : "edu.nau.cs.file.service.dto.ErrorDTO",
            "DeleteObjectResponseDTO": "edu.nau.cs.file.service.dto.DeleteObjectResponseDTO",
            "DeleteChunkResponseDTO" : "edu.nau.cs.file.service.dto.DeleteChunkResponseDTO",
            "FileChunkUploadDTO"     : "edu.nau.cs.file.service.dto.FileChunkUploadDTO",
            "FileChunkDeleteDTO"     : "edu.nau.cs.file.service.dto.FileChunkDeleteDTO",
            "FileObjectDeleteDTO"    : "edu.nau.cs.file.service.dto.FileObjectDeleteDTO",
            "FileChunkDTO"           : "edu.nau.cs.file.service.dto.transfer.FileChunkDTO",
            "FileObjectDTO"          : "edu.nau.cs.file.service.dto.transfer.FileObjectDTO",
            "FolderObjectDTO"        : "edu.nau.cs.file.service.dto.transfer.FolderObjectDTO"
    ]
}

tasks.openApiGenerate.dependsOn tasks.processResources
compileJava.dependsOn tasks.openApiGenerate
sourceSets.main.java.srcDir "$buildDir/generated/src/main/java"
sourceSets.main.resources.srcDir "$buildDir/generated/src/main/resources"
